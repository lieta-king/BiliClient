plugins {
    id 'com.android.application'
}

boolean betaVer = true

android {

    namespace 'com.RobinNotBad.BiliClient'
    compileSdk 32

    sourceSets {
        main{
            jniLibs.srcDirs = ['libs']
        }
    }

    defaultConfig {
        applicationId "com.RobinNotBad.BiliClient"
        minSdk 17
        //noinspection ExpiredTargetSdkVersion
        targetSdk 26
        versionCode 20240608
        versionName "2.6.0"

        if (betaVer) {
            versionName += "-BETA"
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk {
            //noinspection ChromeOsAbiSupport
            abiFilters 'armeabi-v7a','x86'
        }
    }

    signingConfigs {
        release {
            if (project.rootProject.file("local.properties").exists()) {
                Properties pro = new Properties()
                InputStream input = project.rootProject.file("local.properties").newDataInputStream()
                pro.load(input)

                def keyPath = pro.getProperty("KEY_PATH")
                def keyPassword = pro.getProperty("KEY_PASSWORD")
                def aliasName = pro.getProperty("ALIAS_NAME")
                def aliasPassword = pro.getProperty("ALIAS_PASSWORD")

                if (keyPath && keyPassword && aliasName && aliasPassword) {
                    storeFile file(keyPath)
                    storePassword keyPassword
                    keyAlias aliasName
                    keyPassword aliasPassword
                }
            }
        }
    }

    buildTypes {
        release {
            buildConfigField "boolean", "BETA", String.valueOf(betaVer)
            buildConfigField "boolean", "LOG_DEBUG", "true"
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            buildConfigField "boolean", "BETA", String.valueOf(betaVer)
            signingConfig signingConfigs.debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_9
        targetCompatibility JavaVersion.VERSION_1_9
    }

    packagingOptions {
        exclude 'META-INF/androidx.cardview_cardview.version'
    }
    buildFeatures {
        viewBinding false
    }
}

dependencies {
    implementation 'com.google.zxing:core:3.5.0'
    //noinspection GradleDependency
    implementation 'androidx.appcompat:appcompat:1.4.1'
    //noinspection GradleDependency
    implementation 'com.google.android.material:material:1.5.0'
    //noinspection GradleDependency
    implementation 'com.squareup.okhttp3:okhttp:3.12.1'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'com.github.bumptech.glide:glide:4.13.2'
    //noinspection GradleDependency
    implementation 'org.jsoup:jsoup:1.10.2'
    //noinspection GradleCompatible
    implementation 'com.github.chrisbanes:PhotoView:2.3.0'
    implementation 'org.greenrobot:eventbus:3.2.0'
    // Geetest
    implementation 'com.geetest.sensebot:sensebot:4.4.2.1'
    //danmaku
    //implementation 'com.github.ctiao:DanmakuFlameMaster:0.9.25'
    //implementation 'com.github.ctiao:ndkbitmap-armv7a:0.9.21'
    //implementation 'com.github.ctiao:ndkbitmap-x86:0.9.21'
    //ijk
    implementation project(path: ':ijkplayer-java')
    implementation project(path: ':DanmakuFlameMaster')
    implementation 'androidx.asynclayoutinflater:asynclayoutinflater:1.0.0'
}